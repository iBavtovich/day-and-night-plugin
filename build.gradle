buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.intellij.platform' version '2.0.1'
}

group 'com.daynight'
version '2.0.0'

sourceCompatibility = JavaVersion.VERSION_21

dependencies {
    intellijPlatform {
        intellijIdeaCommunity '2024.2.0.1'
        instrumentationTools()

        bundledPlugin 'com.intellij.java'
    }

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    implementation 'com.github.lgooddatepicker:LGoodDatePicker:11.2.1'

    testImplementation 'junit:junit:4.13.2'
}

intellijPlatform {
    pluginConfiguration {
        version = project.version

        changeNotes =
                """
              Make compatible with InteliJ products 24.2<br>
              Get rid of deprecated API usage<br>
              Make working with Java 21<br>
              Support dynamic loading of plugin<br>
              """.stripIndent()
        ideaVersion {
            // Don't patch `untilBuild` version.
            untilBuild = providers.provider({ null })
            sinceBuild = "242.2"
        }
    }
}

runIde {
    jvmArgs = [
            "-XX:+UnlockDiagnosticVMOptions"
    ]
}

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
